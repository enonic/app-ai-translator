import com.github.gradle.node.pnpm.task.PnpmTask

plugins {
    java
    `maven-publish`
    alias(libs.plugins.enonic.defaults)
    alias(libs.plugins.enonic.xp.app)
    alias(libs.plugins.node.gradle)
}

val appName: String by project
val xpVersion: String by project

dependencies {
    implementation(xplibs.api.script)

    include("com.google.auth:google-auth-library-oauth2-http:1.50.0")
    include(xplibs.auth)
    include(xplibs.websocket)
    include(libs.lib.http.client)
    include(xplibs.content)
    include(xplibs.context)
    include(xplibs.task)
    include(libs.lib.cron)
    include(xplibs.schema)
    include(libs.lib.license)

    testImplementation("com.enonic.xp:testing:$xpVersion")
    testImplementation(libs.junit.jupiter)
    testImplementation(libs.mockito.core)
    testRuntimeOnly(libs.junit.platform.launcher)
    testRuntimeOnly(libs.graalvm.js)
}

repositories {
    mavenLocal()
    mavenCentral()
    xp.enonicRepo()
    xp.enonicRepo("dev")
}

node {
    download = true
    version = "24.16.0"
    pnpmVersion = "11.4.0"
}

app {
    name = appName
    systemVersion = xpVersion
}

fun isProd(): Boolean =
    providers.gradleProperty("env").getOrElse("prod") in setOf("p", "prod", "production")

fun environmentShort(): String = if (isProd()) "prod" else "dev"

fun nodeEnvironment(): String = if (isProd()) "production" else "development"

tasks.register<PnpmTask>("pnpmBuild") {
    dependsOn(tasks.named("pnpmInstall"))
    description = "Build UI assets with Vite and server bundles with esbuild."
    args = listOf("run", "build:${environmentShort()}")
    environment = mapOf(
        "FORCE_COLOR" to "true",
        "NODE_ENV" to nodeEnvironment()
    )
    inputs.dir("src/main/resources")
    outputs.dir(layout.buildDirectory.dir("resources/main"))
    outputs.upToDateWhen { false }
}

tasks.register<PnpmTask>("pnpmCheck") {
    dependsOn(tasks.named("pnpmInstall"))
    description = "Run lint, type-check and tests."
    args = listOf("run", "check")
    environment = mapOf("FORCE_COLOR" to "true")
}

tasks.named("jar") {
    dependsOn(tasks.named("pnpmBuild"))
}

tasks.named<Test>("test") {
    dependsOn(tasks.named("pnpmBuild"))
    useJUnitPlatform()
}

val testGraalJs = tasks.register<Test>("testGraalJs") {
    group = "verification"
    description = "Runs tests with the GraalJS script engine."
    dependsOn(tasks.named("pnpmBuild"))
    useJUnitPlatform()
    testClassesDirs = sourceSets["test"].output.classesDirs
    classpath = sourceSets["test"].runtimeClasspath
    systemProperty("xp.script-engine", "GraalJS")
    shouldRunAfter(tasks.named("test"))
}

tasks.named("check") {
    dependsOn(tasks.named("pnpmCheck"))
    dependsOn(testGraalJs)
}

tasks.named<ProcessResources>("processResources") {
    exclude("**/*.ts")
    exclude("**/*.tsx")
    exclude("**/tsconfig*.json")
    exclude("**/assets/index.css")
    includeEmptyDirs = false
    mustRunAfter("pnpmBuild")
}
